plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.protobuf'
    id 'dev.rikka.tools.refine'
    id 'com.google.devtools.ksp'
}

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

String DEFAULT_MANIFEST = "271:https://storage.googleapis.com/music-iq-db/updatable_ytm_db/20240331-030011/manifest.json"
String DEFAULT_MANIFEST_V3 = "3079:https://storage.googleapis.com/music-iq-db/updatable_db_v3/20240324-040013/manifest.json"

def tagName = '2.3.5'
def version = 235

def getKeystoreProperties() {
    def properties = new Properties()
    properties.setProperty("keyAlias", "")
    properties.setProperty("keyPassword", "")
    properties.setProperty("storeFile", "")
    properties.setProperty("storePassword", "")
    try {
        def propertiesFile = rootProject.file("local.properties")
        if (propertiesFile.exists()) {
            properties.load(new FileInputStream(propertiesFile))
        }
    } catch (Exception ignored) {
        properties = null
        println "Unable to read keystore"
    }
    return properties
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.kieronquinn.app.ambientmusicmod"
        minSdk 28
        targetSdk 34
        versionCode version
        versionName tagName

        buildConfigField "String", "DEFAULT_MANIFEST", "\"" + DEFAULT_MANIFEST + "\""
        buildConfigField "String", "DEFAULT_MANIFEST_V3", "\"" + DEFAULT_MANIFEST_V3 + "\""
        buildConfigField "String", "TAG_NAME", "\"${tagName}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def keystore = getKeystoreProperties()
            if (keystore != null) {
                storeFile file(keystore.getProperty("storeFile"))
                storePassword keystore.getProperty("storePassword")
                keyAlias keystore.getProperty("keyAlias")
                keyPassword keystore.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            manifestPlaceholders = [usesCleartextTraffic:"false"]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            manifestPlaceholders = [usesCleartextTraffic:"true"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = 17
    }
    buildFeatures {
        viewBinding true
        aidl true
    }
    namespace 'com.kieronquinn.app.ambientmusicmod'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    //AndroidX
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.lifecycle:lifecycle-service:2.7.0"
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    //Material UI
    implementation 'com.google.android.material:material:1.12.0-rc01'

    //MonetCompat for colours on < S
    implementation 'com.github.KieronQuinn:MonetCompat:0.4.1'

    //Dependency Injection
    implementation "io.insert-koin:koin-android:3.5.3"

    //Protobuf is used in reading data from ASI
    implementation "com.google.protobuf:protobuf-javalite:3.25.2"

    //Lottie for animations
    implementation 'com.airbnb.android:lottie:6.3.0'

    //Used for chip layouts
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    //Audio visualisation during playback
    implementation 'com.github.alxrm:audiowave-progressbar:0.9.2'

    implementation 'com.google.guava:guava:31.1-android'
    implementation 'com.google.code.gson:gson:2.10.1'

    //Update & APK downloading
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //FAQs & update info
    implementation "io.noties.markwon:core:4.6.2"

    //Used in loading album art for ASI
    implementation "com.squareup.picasso:picasso:2.71828"

    //Used to cleanly restart the app in full after setup
    implementation 'com.jakewharton:process-phoenix:2.1.2'

    //Stubs for system APIs (not included in APK)
    compileOnly project(path: ':systemstubs')

    //Astrea service for downloading Superpacks
    implementation project(path: ':astrea')

    //Bypass hidden API restrictions
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    //Shizuku for Shell service
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"
    implementation "dev.rikka.tools.refine:runtime:$refine_version"

    //Dagger + Hilt for injection in Astrea
    implementation "com.google.dagger:hilt-android:2.50"
    implementation 'com.google.dagger:dagger:2.50'
    kapt 'com.google.dagger:dagger-compiler:2.50'
    kapt "com.google.dagger:hilt-compiler:2.50"

    //OSS libraries activity
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'

    //Better Link Movement Method for inline links
    implementation 'me.saket:better-link-movement-method:2.2.0'

    //Autostarter provides some OEM battery checks + starting settings for disabling them
    implementation 'com.github.judemanutd:autostarter:1.1.0'

    //Colour Picker for overlay text
    implementation 'com.github.duanhong169:colorpicker:1.1.6'

    //Room for local song list cache
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}