plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.protobuf'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 28
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.47.0'
        }
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'java'
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    //AndroidX for basic (mainly Annotations), Room and LifecycleService
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation "androidx.lifecycle:lifecycle-service:2.4.1"

    //gRPC for comms + annotations for compiled code
    implementation 'io.grpc:grpc-android:1.46.0'
    implementation 'io.grpc:grpc-binder:1.42.0'
    implementation 'io.grpc:grpc-stub:1.42.0'
    implementation 'io.grpc:grpc-okhttp:1.42.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.46.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    //okhttp for networking
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    //Flogger for logging
    implementation 'com.google.flogger:flogger:0.7.4'
    implementation 'com.google.flogger:google-extensions:0.7.4'

    //Dagger + Hilt for injection
    implementation "com.google.dagger:hilt-android:2.38.1"
    implementation 'com.google.dagger:dagger:2.41'
    kapt 'com.google.dagger:dagger-compiler:2.41'
    kapt "com.google.dagger:hilt-compiler:2.41"

    //Other Google requirements
    implementation "com.google.guava:guava:31.1-android"
    kapt 'com.google.auto.value:auto-value:1.9'
    compileOnly 'com.jakewharton.auto.value:auto-value-annotations:1.4'
    implementation 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    //System stubs (not included in APK) for DeviceConfig, which will be ignored
    compileOnly project(path: ':systemstubs')
}